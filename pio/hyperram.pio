.program hyperram

; HyperRAM interface. 
; based on https://github.com/rscott2049/DECstation2040/blob/main/sw/psram/libhyperram/hyperram.pio
; Pin mapping:
; - in/out pins are mapped to DQx
; - sideset pin is mapped to {CK}
; - set pins are mapped to RWDS (msb), CSn (lsb)
; - jmp pin mapped to RWDS

; Make side sets optional to enable pin sharing with other SMs
.side_set 1 opt

; Settings: 150 MHz, clkdiv = 1, lat = 3;
; .define public LATENCY 4
; .define public LAT_SHORT 1

; Settings: 266 MHz, clkdiv = 1, lat = 5;
.define public LATENCY 8
.define public LAT_SHORT 3

public start:
    set x, 0                            ; Flag for no work
    pull block                        ; Peek into FIFO
    out x, 8                            ; Set length for CMD/ADR phase     

    set pins, 0b000       side 0b0       ; Enable chip select
    set pindirs, 0b110    side 0b0 [1]   ; Make RWDS pin input, CS output
    out pindirs, 8       side 0b0       ; Set CMD/Data pins direction
    set y, LATENCY       side 0b0       ; Assume maximum latency     
    jmp pin, setup       side 0b0       ; Skip if RWDS set for max latency
    set y, LAT_SHORT     side 0b0       ; Short latency
setup:
adr:
    out pins, 8          side 0b0 [4]   ; Setup write of cmd/adr
    nop                  side 0b1       ; CA phase 0 write
    out pins, 8          side 0b1 [4] 
    jmp x--, adr         side 0b0       ; CA phase 1 write

    out x, 16            side 0b0 [4]   ; Get transfer len
    out pindirs, 8       side 0b1       ; Set xfer pin dir
    out pc, 8            side 0b1 [4]   ; Jump to command [start, r/w_lat]

; For last tick sample 
public r_lat:
lat_cnt:
    nop                  side 0b0 [5]   ; Wait for latency to expire
    jmp y--, lat_cnt     side 0b1 [5]

public r_data:
;    in pins, 8           side 0b0 [5]    ; CA phase 0 
    in pins, 8           side 0b0 [6]    ; CA phase 0 
    in pins, 8           side 0b0        ; CA phase 1
    jmp x--, r_data      side 0b1 [5]    ; Continue until all data captured 
    jmp done             side 0b0

w_lat_cnt:
    set pindirs, 0b111    side 0b0 [5]    ; Make RWDS pin output 
public w_lat:
    jmp y--, w_lat_cnt   side 0b1 [5]

public w_data:
    out pins, 8          side 0b0 [4]    ; Setup write of cmd/adr
    nop                  side 0b1        ; CA phase 0 write, setup RWDS ph 1
    out pins, 8          side 0b1 [4] 
    jmp x--, w_data      side 0b0        ; CA phase 1 write

done:
public finish:
    set pins, 0b100       side 0b0        ; Deselect part, idle clock

